buildscript {
    

    repositories {
        mavenLocal()
        mavenCentral()
        gradlePluginPortal()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        google()
    }

    dependencies {
        classpath "com.badlogicgames.gdx:gdx-tools:1.12.1"
    }
}

import com.badlogic.gdx.tools.texturepacker.TexturePacker

task texturePacker {
    doLast {
        logger.info("Calling TexturePacker");
        TexturePacker.process("raw_animations/", "assets/animations/", "animation_atlas");
    }
}

task writeAllAssetsToFile {
    doLast {
        def assetsFolder = new File("${project.rootDir}/assets/")
        def assetsFile = new File(assetsFolder, "assets.txt")
        assetsFile.delete()

        fileTree(assetsFolder).collect {assetsFolder.relativePath(it) }.each {
            assetsFile.append(it + "\0")
        }
    }
}

def versionFromEnv = System.getenv("VERSION")
def versionStr = versionFromEnv ?: "DEVELOPMENT"

def assetsFolder = new File("${project.rootDir}/assets/")
def versionFile = new File(assetsFolder, "version.info")

versionFile.delete();
versionFile.append(versionStr);

allprojects {
    apply plugin: "eclipse"

    version = versionStr
    ext {
        appName = "SCNGame"
        gdxVersion = '1.12.1'
        roboVMVersion = '2.3.20'
        box2DLightsVersion = '1.5'
        ashleyVersion = '1.7.4'
        aiVersion = '1.8.2'
        gdxControllersVersion = '2.2.3'
    }

    repositories {
        mavenLocal()
        mavenCentral()
        google()
        gradlePluginPortal()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
        maven { url "https://jitpack.io" }
    }
}

project(":desktop") {
    apply plugin: "java-library"


    dependencies {
        implementation project(":core")
        implementation "com.badlogicgames.gdx-controllers:gdx-controllers-desktop:$gdxControllersVersion"
        api "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
        api "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
        
    }
}

project(":core") {
    apply plugin: "java-library"

    dependencies {
        implementation "com.badlogicgames.gdx-controllers:gdx-controllers-core:$gdxControllersVersion"
        api "com.badlogicgames.gdx:gdx:$gdxVersion"
        api "com.github.tommyettinger:jbump:1.0.1"
        
    }

    compileJava.dependsOn writeAllAssetsToFile;
    compileJava.dependsOn texturePacker;

}
